# Maintainer: Thomas Fanninger <thomas@fanninger.at>

pkgname=gogs-git
_pkgname=gogs
pkgver=20140902
pkgrel=1
pkgdesc="Gogs(Go Git Service) is a Self Hosted Git Service in the Go Programming Language. This is the current git version from branch master."
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="http://gogits.org/"
license=('MIT')
depends=('git')
optdepends=('sqlite: SQLite support'
            'mariadb: MariaDB support'
            'postgresql: PostgreSQL support'
            'redis: Redis support'
            'memcached: MemCached support'
            'openssh: GIT over SSH support')
makedepends=('go>=1.2' 'git' 'mercurial' 'patch')
conflicts=('gogs-bin' 'gogs' 'gogs-git-dev')
options=('!strip' '!emptydirs')
backup=('srv/gogs/conf/app.ini')

install=gogs.install

source=('gogs.service'
        'app.ini.patch'
        'start.sh'
        'helper.sh'
        "$_pkgname::git+http://github.com/gogits/gogs.git")

sha512sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

_goroot="/usr/lib/go"

pkgver(){
  cd "$startdir/$_pkgname"
  echo $(git rev-list --count master)
}

prepare() {
  source $startdir/helper.sh

  export GOROOT=/usr/lib/go

  msg2 "Prepare GO build enviroment"

  rm -rf build
  mkdir -p build/go
  cd build/go

  for f in "$GOROOT/"*; do
    ln -s "$f"
  done

  rm pkg
  mkdir pkg
  cd pkg

  for f in "$GOROOT/pkg/"*; do
    ln -s "$f"
  done

  export GOROOT="$srcdir/build/go"
  export GOPATH="$srcdir/build"
	
  mkdir -p "$GOPATH/src/github.com/gogits"
  mv "$srcdir/$_pkgname" $GOPATH/src/github.com/gogits/gogs 

  msg2 "Check and download dependencies from .gopmfile"
  get_gopm "$GOPATH/src/github.com/gogits/gogs/.gopmfile" "$GOPATH/src"

  # Execute patch
  msg2 "Execute patches"
  patch -Np1 -i "$srcdir/app.ini.patch" "$GOPATH/src/github.com/gogits/gogs/conf/app.ini"
}

build() {
  cd $GOPATH/src/github.com/gogits/gogs

  msg2 "Build GO program"
  go fix
  go build -x -tags='sqlite redis memcache'
}

package() {
  mkdir -p "$pkgdir/usr/share/$_pkgname/"
  cp "$startdir/start.sh" "$pkgdir/usr/share/$_pkgname/"
  chmod +x "$pkgdir/usr/share/$_pkgname/start.sh"
  cp "$srcdir/build/src/github.com/gogits/gogs/gogs" "$pkgdir/usr/share/$_pkgname/"
  chmod +x "$pkgdir/usr/share/$_pkgname/gogs"
  cp -r "$srcdir/build/src/github.com/gogits/gogs/conf" "$pkgdir/usr/share/$_pkgname"
	mkdir -p "$pkgdir/usr/share/themes/goges/default/"
  cp -r "$srcdir/build/src/github.com/gogits/gogs/public" "$pkgdir/usr/share/themes/goges/default"
  cp -r "$srcdir/build/src/github.com/gogits/gogs/templates" "$pkgdir/usr/share/themes/goges/default"
 
  mkdir -p "$pkgdir/srv/$_pkgname/conf"
  cp "$pkgdir/usr/share/$_pkgname/conf/app.ini" "$pkgdir/srv/$_pkgname/conf/app.ini"

  mkdir -p "$pkgdir/usr/lib/systemd/system/"
  cp -r "$startdir/gogs.service" "$pkgdir/usr/lib/systemd/system/"

  mkdir -p "$pkgdir/usr/share/licenses/$_pkgname"
  cp "$srcdir/build/src/github.com/gogits/gogs/LICENSE" "$pkgdir/usr/share/licenses/$_pkgname"
}