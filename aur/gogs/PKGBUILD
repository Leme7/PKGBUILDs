# Contributor: Thomas Laroche <tho.laroche@gmail.com>
# Contributor: Kristian Klausen <klausenbusk@hotmail.com>
# Maintainer: Thomas Fanninger <thomas@fanninger.at>

pkgname=gogs
_pkgname=${pkgname}
pkgver=0.5.13
pkgrel=1
epoch=1
pkgdesc="Gogs(Go Git Service) is a Self Hosted Git Service in the Go Programming Language."
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
changelog=$pkgname.changelog
url="http://gogs.io/"
license=('MIT')
depends=('git')
optdepends=('sqlite: SQLite support'
            'mariadb: MariaDB support'
            'postgresql: PostgreSQL support'
            'redis: Redis support'
            'memcached: MemCached support'
            'openssh: GIT over SSH support')
makedepends=('go>=1.2' 'git' 'mercurial' 'patch')
conflicts=('gogs-bin' 'gogs-git' 'gogs-git-dev')
options=('!strip' '!emptydirs')
backup=('srv/gogs/conf/app.ini')

install=gogs.install

_gourl=github.com/gogits
source=('gogs.service'
        'app.ini.patch'
	    'https://github.com/gogits/gogs/commit/685ed1f8075c0fedc2cfaf0f8c43f9439132e1da.patch'
	    'https://github.com/gogits/gogs/commit/f625665d8d72fdc4d2ce28a88aaaf01eaf5c2370.patch'
        "${_pkgname}-${pkgver}::git+https://${_gourl}/${_pkgname}.git#tag=v${pkgver}")

sha512sums=('2b4303f850e3b13b2fc3c9f0bc5820dae431d228002b35f01be0d4bfbcf05de8dcec2a559a85e318b609e4a4d492d44306eadf5f6508fd72333b198661bb0bb7'
            '0286df0a5cfec815b9ff5f5e22dd9e7b6b4bb98b0d119cdd31474183168500e7a036722040a73276f6291d9768fa5565866ae360c17b3442de801320a275d712'
            'd16080114cf9dff74747eda30f3b6aa16d77528233644d5f3d44a83c116c25aa6bb22dcace6a426fcadfac3f6587cd9b188a7968435b778e01a0d36615f298bc'
            'e27c2d4dbf71f011736521b4e7d40cbb70a3d35382a97308172fbc26536239b6fba0959030c7e36690bb62b1775bbe55ec95f01a6304ce1fc1759aba78ed48a9'
            'SKIP')


prepare() {
  mkdir -p "${srcdir}/src/${_gourl}"
  mv "${_pkgname}-${pkgver}" "${srcdir}/src/${_gourl}/${_pkgname}"
  msg2 "go get"
  GOPATH="${srcdir}" go get -tags "sqlite redis memcache" "${_gourl}/${_pkgname}"
  
  msg2 "Patch: GOGS app.ini"
  patch -Np1 -i "${srcdir}/app.ini.patch" "${srcdir}/src/${_gourl}/${_pkgname}/conf/app.ini"
  msg2 "Patch: models: fix XORM API break"
  cd "${srcdir}/src/${_gourl}/${_pkgname}"
  patch -Np1 -i "${srcdir}/685ed1f8075c0fedc2cfaf0f8c43f9439132e1da.patch"
  msg2 "Patch: modules/setting: add abs path check before add workdir prefix"
  patch -Np1 -i "${srcdir}/f625665d8d72fdc4d2ce28a88aaaf01eaf5c2370.patch"
}

build() {
  msg2 "Build program"
  cd ${srcdir}/src/${_gourl}/${_pkgname}
  GOPATH="${srcdir}" go fix
  GOPATH="${srcdir}" go build -tags "sqlite redis memcache"
}

package() {
  install -D -m 0755 "${srcdir}/src/${_gourl}/${_pkgname}/${_pkgname}" "${pkgdir}/usr/share/${_pkgname}/${_pkgname}"

  cp -r "${srcdir}/src/${_gourl}/${_pkgname}/conf" "${pkgdir}/usr/share/${_pkgname}"
  install -d "${pkgdir}/usr/share/themes/gogs/default/"
  cp -r "${srcdir}/src/${_gourl}/${_pkgname}/public" "${pkgdir}/usr/share/themes/gogs/default"
  cp -r "${srcdir}/src/${_gourl}/${_pkgname}/templates" "${pkgdir}/usr/share/themes/gogs/default"

  install -D -m 0600 "${pkgdir}/usr/share/${_pkgname}/conf/app.ini" "$pkgdir/srv/${_pkgname}/conf/app.ini"
  install -D -m 0644 "${srcdir}/gogs.service" "$pkgdir/usr/lib/systemd/system/gogs.service"
  install -d "${pkgdir}/var/log/gogs"
  install -D -m 0644 "${srcdir}/src/${_gourl}/${_pkgname}/LICENSE" "${pkgdir}/usr/share/licenses/$_pkgname"
}
